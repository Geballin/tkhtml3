#
# tree.test --
#
#     Test that html documents are parsed to the correct tree structure.
#
# TODO: Copyright etc.
#

# Standard stuff.
set INTEST 1
set testdir [file dirname [info script]]
source $testdir/autotest.test

html .h

proc node {args} {
  set ret [lindex $args 0]
  if {[llength $args]>1} {
    set ret [list $ret [lrange $args 1 end]]
  }
  return $ret
}

proc test_tree {testname html tree} {
    ::tcltest::test $testname {} {
        .h clear
        .h parse $html
        .h tree get
    } $tree
}

# Test a very simple document, entirely XHTML correct.
#
test_tree tree-1.1 {
    <html>
        <head>
        </head>
        <body>
            <p>First test case</p>
        </body>
    </html>
} [
    node html [
        node head ] [
        node body [
            node p [
                node "First test case" ] ] ]
]

# The same document, but with the </p> closing tag omited. In
# this case the <p> element is auto-closed when the closing
# tag </body> is encountered.
#
test_tree tree-1.2 {
    <html>
        <head>
        </head>
        <body>
            <p>First test case
        </body>
    </html>
} [
    node html [
        node head ] [
        node body [
            node p [
                node "First test case" ] ] ]
] 

# The same document, but truncated just before the </p> tag.
# All open tags are auto-closed when the end of the document
# is reached.
#
test_tree tree-1.3 {
    <html>
        <head>
        </head>
        <body>
            <p>First test case
} [
    node html [
        node head ] [
        node body [
            node p [
                node "First test case" ] ] ]
] 

# A similar document, but this time we auto-close the <p>
# element by opening another HTML 'block' level element - in 
# this case another <p>. The second <p> should be a sibling
# of the first, not a child.
#
test_tree tree-1.4 {
    <html>
        <head>
        </head>
        <body>
            <p>First test case
            <p>Second paragraph
        </body>
    </html>
} [
    node html [
        node head ] [
        node body [
            node p [
                node "First test case" ] ] [
            node p [
                node "Second paragraph" ] ] ]
] 

# The same document as above, but leave out the <html> element. It should
# be automaticly inserted into the tree anyway.
#
test_tree tree-1.5 {
        <head>
        </head>
        <body>
            <p>First test case
            <p>Second paragraph
        </body>
} [
    node html [
        node head ] [
        node body [
            node p [
                node "First test case" ] ] [
            node p [
                node "Second paragraph" ] ] ]
] 

# TODO: Need to test that the following elements are auto-closed
# correctly: <colgroup>, <dd>, <dt>, <li>, <option>, <p>, <td>
# <tfoot>, <th>, <thead>, <tr>.

# TODO: Need to test that if they are not present, the <body>, 
# <head> and <tbody> tags are inserted into the tree automatically.

destroy .h
